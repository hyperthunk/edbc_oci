#!/usr/bin/env escript
%% -----------------------------------------------------------------------------
%%
%% EDBC OCI - Configure Script
%%
%% Copyright (c) 2011 Tim Watson (watson.timothy@gmail.com)
%%
%% Permission is hereby granted, free of charge, to any person obtaining a copy
%% of this software and associated documentation files (the "Software"), to deal
%% in the Software without restriction, including without limitation the rights
%% to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
%% copies of the Software, and to permit persons to whom the Software is
%% furnished to do so, subject to the following conditions:
%%
%% The above copyright notice and this permission notice shall be included in
%% all copies or substantial portions of the Software.
%%
%% THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
%% IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
%% FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
%% AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
%% LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
%% OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
%% THE SOFTWARE.
%% -----------------------------------------------------------------------------
%%

main(Args) ->
    Options = parse_args(Args),
    case lists:keymember(help, 1, Options) of
        true ->
            help(), halt(0);
        false ->
            configure(Options)
    end.

configure(Options) ->
    io:format("~s~n", [printable(Options)]).

parse_args(Args) ->
    lists:flatten(lists:foldl(fun(Arg, Conf) -> [parse(Arg)|Conf] end, [], Args)).

parse(Arg) when is_list(Arg) ->
    first_match(Arg, args()).

first_match(Arg, []) ->
    exit_badarg(Arg);
first_match(Arg, [Opt|Rest]) ->
    case match(Arg, Opt) of
        undefined ->
            first_match(Arg, Rest);
        Match ->
            Match
    end.

match(Arg, {Rx, Settings, Captures, Filter}) ->
    case re:run(Arg, Rx, [global, {capture, Captures,list}]) of
        {match, [Matches]} when is_list(Matches) ->
            case Settings of
                S when is_list(S) ->
                    lists:zip(filter(Matches, Filter), Settings);
                Merge when is_function(Merge) ->
                    Merge(filter(Matches, Filter));
                Other ->
                    abort("Invalid Settings: ~p~n", [Other])
            end;
        _Other ->
            exit_badarg(Arg)
    end.

exit_badarg(Arg) ->
    abort("Unrecognised Option(s) ~s~n", [Arg]).

help() ->
    io:format(
"`configure' configures this package to adapt to any supported system.

Usage: ./configure [OPTIONS]

Options:~n"),
    Indent = "    ",
    F = fun({Thing, Comments}, {Rx, _, C, _}) ->
            Text = rejoin_option(Thing, Rx, C),
            Spacer = lists:concat(lists:duplicate(28 - length(Text), " ")),
            io:format("~s~s~s~s~n", [Indent, Text, Spacer, Comments]);
           (break, _) ->
            io:format("~n");
           (Thing2, _) ->
            io:format("~s~p~n", [Indent, Thing2])
        end,
    [ F(I, A) || A <- args(), I <- (element(4, A) ++ [break]) ].

rejoin_option(Thing, Rx, C) ->
    case re:run(Rx, "\\(\?\<([^\>]+)\>[^\\)]*\\)", [global, {capture, [1], list}]) of
        {match, Matches} ->
            %% NB: our ordering comes from the matches themselves
            Indexes = erlang:tl([ string:str(C, [list_to_atom(lists:nth(1, M))]) ||
                                   M <- Matches ]),
            lists:foldl(fun(Idx, Acc) when Idx >= 0 ->
                            re:replace(Acc, "(\\([^\\)]*\\))",
                                       string:to_upper(atom_to_list(lists:nth(Idx, C))),
                                       [ungreedy, {return,list}]);
                           (_, Acc) -> Acc
                        end,
                        re:replace(Rx, "(\\([^\\)]*\\))", Thing, [ungreedy, {return,list}]),
                        Indexes);
        _ ->
            Rx
    end.

args() ->
    [
        {"--(?<option>.*)",
            fun([X]) -> {erlang:list_to_atom(X), enabled} end, [option],
            [{"verbose", "Print lots of info out during configure process"},
             {"help", "Print out help and exit"}]},
        {"--(?<option>.*)=(?<value>.*)", fun erlang:list_to_tuple/1,
        	[option, value],
            [{"prefix", "Base install directory "
                        "(default to ERL_LIBS or code:lib_dir)"}]},
        {"--enable-(?<thing>.*)", [enabled], [thing],
            [{"64bit", "Enable 64bit mode (ignored where not applicable)"},
             {"64bit-darwin", "Default for Snow Leopard and above"},
             {"drcp", "Enable DRCP (OCI >= 11g only)"}]},
        {"--disable-(?<thing>.*)", [disabled], [thing],
            [{"64bit", "Force 32bit mode"},
             {"64bit-darwin", "Force 32bit mode on darwin-64"},
             {"drcp", "Disable Database Resident Connection Pooling "
                      "(default for OCI < 11g)"}]},
        {"--with-(?<thing>.*)=(?<where>.*)", fun erlang:list_to_tuple/1, 
        		[thing, where],
            [{"oci", "Location of the OCI install"},
             {"oracle-home", "Override ORACLE_HOME environment variable"}]}
    ].

filter(M, []) ->
    M;
filter(Match=[H|_], Filter) when is_list(Match) andalso is_integer(H) ->
    case lists:any(fun(X) -> X == Match end, allowed(Filter)) of
        false ->
            abort("Unrecognised Option(s) ~p~n", [Match]);
        true ->
            Match
    end;
filter(Matches, Filter) ->
    lists:map(fun(X) -> filter(X, Filter) end, Matches).

allowed(Filter) ->
    lists:map(fun({E,_}) -> E;(N) -> N end, Filter).

printable(Term) ->
    erl_prettypr:format(erl_parse:abstract(Term)).

abort(Msg, Args) ->
    io:format(Msg, Args), halt(1).
